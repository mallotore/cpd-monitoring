apply plugin: 'groovy'
apply plugin: 'application'

group = "com.mallotore"
version = "0.1"
description = "Wrapper for Sigar"

repositories {
    mavenCentral()
    jcenter()
}

configurations {
  nativeLibs
}

sourceSets {
    main.groovy.srcDir 'src'
    test.groovy.srcDir 'test'
}

dependencies {
    runtime group: 'org.slf4j', name: 'slf4j-nop', version: '1.6.4'

    nativeLibs ("org.fusesource:sigar:1.6.4:native") {
        exclude group: "log4j"
    }
    
    compile 'org.fusesource:sigar:1.6.4'
    compile 'org.codehaus.groovy:groovy-all:2.4.5'
    testCompile "org.spockframework:spock-core:1.0-groovy-2.4"
    // optional dependencies for using Spock
    testCompile "org.hamcrest:hamcrest-core:1.3" // only necessary if Hamcrest matchers are used
    testRuntime "cglib:cglib-nodep:3.1"          // allows mocking of classes (in addition to interfaces)
    testRuntime "org.objenesis:objenesis:2.1"    // allows mocking of classes without default constructor (together with CGLIB)
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

task extractNativeJar(type: Copy) {
  configurations.nativeLibs.each {
    from zipTree(it)
  }
  into 'native-libs'
}

mainClassName = 'com.mallotore.Main'

run {
  dependsOn 'extractNativeJar'
  jvmArgs = [ '-Dorg.hyperic.sigar.path=./native-libs' ]
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
    }
}

test{
   dependsOn 'extractNativeJar'
   jvmArgs = [ '-Dorg.hyperic.sigar.path=./native-libs' ] 
}

startScripts {
    doLast {
        unixScript.text = unixScript.text.replace('-classpath', ' -Dorg.hyperic.sigar.path=$APP_HOME/native-dependencies -Dcom.sun.management.jmxremote -Dcom.sun.management.jmxremote.port=1617 -Dcom.sun.management.jmxremote.authenticate=false -Dcom.sun.management.jmxremote.ssl=false -classpath')
        windowsScript.text = windowsScript.text.replace('-classpath', ' -Dorg.hyperic.sigar.path=$APP_HOME/native-dependencies -Dcom.sun.management.jmxremote -Dcom.sun.management.jmxremote.port=1617 -Dcom.sun.management.jmxremote.authenticate=false -Dcom.sun.management.jmxremote.ssl=false -classpath')
    }
}

distZip {
    into("${project.name}-${version}") {
        from '.'
        include 'native-dependencies/*'
    }
}

distTar {
    into("${project.name}-${version}") {
        from '.'
        include 'native-dependencies/*'
    }
}